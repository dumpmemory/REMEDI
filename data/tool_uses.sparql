SELECT ?itemLabel (GROUP_CONCAT(?useLabel; SEPARATOR="|") AS ?uses) (GROUP_CONCAT(?subclassLabel; SEPARATOR="|") AS ?subclasses)
WHERE
{
  {
    SELECT DISTINCT ?item ?itemLabel ?useLabel ?subclassLabel
    WHERE
    {
      # Anything that is a descendent of "tool"
      {
        ?item wdt:P31 wd:Q39546
        FILTER(
          ?item NOT IN (
            wd:Q108375203
          )
        )
      }
      UNION
      {
        ?item (wdt:P31|wdt:P279)* ?subclass.
        ?subclass wdt:P279 wd:Q39546.
        FILTER(
          ?subclass NOT IN (
            wd:Q6657015,
            wd:Q18643213,
            wd:Q728,
            wd:Q933870,
            wd:Q1183543,
            wd:Q6554101,
            wd:Q2041172,
            wd:Q1074814,
            wd:Q44575957,
            wd:Q11019,
            wd:Q134574,
            wd:Q152910,
            wd:Q2002016,
            wd:Q24651263,
            wd:Q987767,
            wd:Q1476837
          )
        )
      }

      # Must have a use.
      ?item wdt:P366 ?use.

      # Must have an English label.
      SERVICE wikibase:label
      {
        bd:serviceParam wikibase:language "en"
      }
    }
  }
  FILTER(LANGMATCHES(LANG(?itemLabel), "en") && LANGMATCHES(LANG(?useLabel), "en"))
}
GROUP BY ?itemLabel
HAVING (COUNT(?useLabel) > 1)
